
/*
 * This file is part of Sympathy
 * Copyright (c) 2016-2018 Job and Esther Technologies Oy
 * Copyright (c) 2018 Eqela Oy
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License, version 3,
 * as published by the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

import jk.fs
import jk.log
import jk.console
import symlib.report

class is ConsoleApplicationMain:

main:
	return executeMain(new this(), args)

func execute override as bool
{
	var reportFile as File
	var pdfFile as File
	var recordFile as File
	var formulas = new vector<string>
	foreach arg in args {
		if arg.isFlag("debug") {
			ctx.setEnableDebugMessages(true)
		}
		else if arg.isOption("records") {
			recordFile = File.forPath(arg.value)
		}
		else if arg.isOption("formula") {
			formulas += arg.value
		}
		else if arg.isParameter() {
			if reportFile == null {
				reportFile = File.forPath(arg.parameter)
			}
			else if pdfFile == null {
				pdfFile = File.forPath(arg.parameter)
			}
			else {
				Log.error(ctx, "Too many files specified.")
				return false
			}
		}
		else {
			arg.reportAsUnsupported(ctx)
			return false
		}
	}
	assert reportFile:
		Log.error(ctx, "No report file specified.")
	var ds as ReportDataSource
	if recordFile {
		assert ds = StaticFileDataSource.forFile(recordFile):
			Log.error(ctx, "Failed to read data file: `" .. recordFile.getPath() .. "'")
	}
	Log.status(ctx, "Reading report file ..")
	var report = assert XMLReportParser.parseFile(ctx, reportFile):
		Log.error(ctx, "Failed to read report: `" .. reportFile.getPath() .. "'")
	Log.info(ctx, "Report file successfully read: `" .. reportFile.getPath() .. "'")
	foreach formula in formulas {
		var cc = String.split(formula, ':', 2)
		assert report.setFormulaValue(cc[0], cc[1]):
			Log.error(ctx, "Failed to set formula value `" .. cc[0] .. "' to `" .. cc[1] .. "'")
	}
	if pdfFile {
		Log.status(ctx, "Generating PDF ..")
		var pr = new ReportProcessor()
		pr.setCtx(ctx)
		pr.setReport(report)
		pr.setDataSource(ds)
		assert pr.processToPdfFile(pdfFile):
			Log.error(ctx, "Failed to generate PDF: `" .. pdfFile.getPath() .. "'")
		Log.info(ctx, "PDF report successfully generated: `" .. pdfFile.getPath() .. "'")
	}
	return true
}
